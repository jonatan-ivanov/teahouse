ext.buildTime = new Date().toInstant()

buildscript {
    ext.springBootVersion = '2.4.2'
    ext.springCloudVersion = '2020.0.0'
    ext.springDataVersion = '2020.0.3'

    repositories {
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'org.ajoberstar.grgit:grgit-gradle:4.1.0'
        classpath 'gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:2.2.4'
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
    }
}

task clean

apply plugin: 'org.ajoberstar.grgit'
apply from: "$rootDir/gradle/idea.gradle"

allprojects {
    task cleanAll {
        description = 'Deletes the build directory and the IDE files and directories'
        group = 'build'
        dependsOn clean, cleanIdea, cleanIdeaWorkspace
        doLast {
            delete 'out', '.idea'
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply from: "$rootDir/gradle/dependency-locking.gradle"

    sourceCompatibility = 15
    targetCompatibility = 15

    group = 'org.example.teahouse'
    version = "${grgit.head().abbreviatedId}.${buildTime.toEpochMilli()}"

    repositories {
        mavenCentral()
    }

    dependencies {
        ['annotationProcessor', 'compileOnly', 'implementation', 'runtimeOnly', 'testImplementation'].each { conf ->
            dependencies.add(conf, platform("org.springframework.boot:spring-boot-dependencies:$springBootVersion"))
            dependencies.add(conf, platform("org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion"))
            dependencies.add(conf, platform("org.springframework.data:spring-data-bom:$springDataVersion"))
            dependencies.add(conf, platform('org.zalando:logbook-bom:2.+'))
        }
        testImplementation platform('org.junit:junit-bom:5.+')

        annotationProcessor 'org.projectlombok:lombok'
        compileOnly 'org.projectlombok:lombok'

        testImplementation 'org.junit.jupiter:junit-jupiter-api'
        testImplementation 'org.junit.jupiter:junit-jupiter-params'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    }

    jar.manifest.attributes(
        'Name': project.name,
        'Implementation-Build-Date': buildTime,
        'Implementation-Title': project.name,
        'Implementation-Version': project.version,
        'Implementation-Vendor': project.group,
        'Commit-AbbreviatedId': grgit.head().abbreviatedId,
        'Commit-Id': grgit.head().id,
        'Commit-Date': grgit.head().getDate().toInstant(),
        'Source-Path': grgit.remote.list().findResult { it.name == 'origin' ? it.url : null },
        'Created-By': "Gradle ${gradle.gradleVersion} with Gradle Ghost (grg)",
        'Built-By': System.properties['user.name'],
        'JDK': org.gradle.internal.jvm.Jvm.current(),
        'OS': org.gradle.internal.os.OperatingSystem.current()
    )

    test {
        useJUnitPlatform()
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}
